//===== rAthena Script =======================================
//= NPC de Reset
//===== Descrição: ===========================================
//= Reseta habilidades, estatísticas ou ambos.
//= TRADUZIDO POR Typer.me/@Systemic404
//===== Comentários Adicionais: ==============================
//= 1.0 Primeira Versão
//= 1.1 Otimizado para o bem maior. [Kisuka]
//= 1.2 Limpeza [Euphy]
//= 1.3 Todas as condições removidas ao resetar habilidades. [Euphy]
//= 1.4 Script comprimido, adicionada opção de limite de uso [Stolao]
//=     Mudança de set -> setarray, Melhorado texto com F_InsertPlural
//= 1.5 Adicionado sc_end_class para remover alterações de status relacionadas [sader1992]
//============================================================
prontera,150,193,4	script	Reset Girl	124,{
	//	 		Habilidades,	Estatísticas,	Ambos,	Limite
	setarray .@Reset,	5000,	5000,	9000,	0;
	mes "[Reset Girl]";
	if(.@Reset[3] && reset_limit > .@Reset[3]) {
		mes "Desculpe, você só pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vez")+" na sua vida.";
		close;
	}
	mes "Eu sou a Reset Girl.";
	mes "Resetar Estatísticas: "+ callfunc("F_InsertComma",.@Reset[1]) +"z";
	mes "Resetar Habilidades: "+ callfunc("F_InsertComma",.@Reset[0]) +"z";
	mes "Resetar Ambos: "+ callfunc("F_InsertComma",.@Reset[2]) +"z";
	if(.@Reset[3]) mes "Você só pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vez")+", então use "+((.@Reset[3]>1)?"elas":"isso")+" sabiamente.";
	mes "Por favor, selecione o serviço desejado:";
	next;
	set .@i,(select("^FF3355Resetar Habilidades:Resetar Estatísticas:Resetar Ambos^000000:Cancelar"));
	if(.@i > 3) close;
	mes "[Reset Girl]";
	if (Zeny < .@Reset[.@i-1]) {
		mes "Desculpe, você não tem Zeny suficiente.";
		close;
	}
	if(.@Reset[3]){
		mes "Você só pode resetar "+callfunc("F_InsertPlural",.@Reset[3],"vez")+" na sua vida, tem certeza?";
		if(select("Deixe-me pensar:Está bem") == 1) close;
	}
	set Zeny, Zeny-.@Reset[.@i-1];
	if(.@i&1){
		sc_end_class;
		ResetSkill;
	}
	if(.@i&2) ResetStatus;
	mes "Aqui está!";
	if(.@Reset[3]) set reset_limit,reset_limit + 1;
	close;
}
